buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

configurations {
	deployJars
}

apply plugin: 'maven'
apply plugin: 'forge'
apply plugin: 'eclipse'

version = "2.0.4.5"
group= "net.slimevoid"
archivesBaseName = "SlimevoidLibrary"

minecraft {
    version = "1.7.2-10.12.0.1024"
    assetDir = "eclipse/assets"
}
processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

dependencies {
    deployJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

uploadArchives {
    repositories.mavenDeployer {
    	configuration = configurations.deployJars
        if (project.hasProperty("slimevoidUsername")) {
            snapshotRepository(url: "scp://hydadedi.playat.ch/maven/snapshots/") {
                authentication(userName: "${slimevoidUsername}", password: "${slimevoidPassword}")
            }
            repository(url: "scp://hydadedi.playat.ch/maven/repos/") {
                authentication(userName: "${slimevoidUsername}", password: "${slimevoidPassword}")
            }
        } else {
            snapshotRepository(url: 'file://localhost/' + project.file('build/snapshot').getAbsolutePath())
            repository(url: 'file://localhost/' + project.file('build/repo').getAbsolutePath())
        }
        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'SlimevoidLibrary'
            url 'https://github.com/Slimevoid/SlimevoidLibrary'

            scm {
                url 'https://github.com/Slimevoid/SlimevoidLibrary'
                connection 'scm:git:git://github.com/Slimevoid/SlimevoidLibrary.git'
                developerConnection 'scm:git:git@Slimevoid/SlimevoidLibrary.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/Slimevoid/SlimevoidLibrary/issues'
            }

            licenses {
                license {
                    name 'Lesser General Public Licence'
                    url 'https://raw.github.com/Slimevoid/SlimevoidLibrary/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Eurymachus'
                    name 'Eurymachus'
                    roles { role 'developer' }
                }
                developer {
                    id 'Tarig0'
                    name 'Tarig'
                    roles { role 'contributor' }
                }
            }
        }
    }
}
